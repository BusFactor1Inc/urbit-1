!:  ::  %behn, agent execution
!?  163
::::
|=  pit=vase
=>  =~
|%  ::::::::::::::::::::::::::::::::::::::::::::::::::::::    rest of arvo
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::
++  hood                                                ::  assembly plan
  $:  zus=@ud                                           ::  zuse kelvin
      sur=(list hoot)                                   ::  structures
      lib=(list hoof)                                   ::  libraries
      fan=(list horn)                                   ::  resources
      src=(list hoop)                                   ::  program
  ==                                                    ::
++  hoof  (pair term (unit (pair case ship)))           ::  resource reference
++  hoot  (pair bean hoof)                              ::  structure gate/core
++  hoop                                                ::  source in hood
  $%  [%& p=twig]                                       ::  direct twig
      [%| p=beam]                                       ::  resource location   
  ==                                                    ::
++  horn                                                ::  resource tree
  $%  [%ape p=twig]                                     ::  /~  twig by hand
      [%arg p=twig]                                     ::  /$  argument
      [%day p=horn]                                     ::  /|  list by @dr
      [%dub p=term q=horn]                              ::  /=  apply face
      [%fan p=(list horn)]                              ::  /.  list
      [%for p=path q=horn]                              ::  /,  descend
      [%hel p=@ud q=horn]                               ::  /%  propagate heel
      [%hub p=horn]                                     ::  /@  list by @ud
      [%man p=(map span horn)]                          ::  /*  hetero map
      [%nap p=horn]                                     ::  /_  homo map
      [%now p=horn]                                     ::  /&  list by @da
      [%saw p=twig q=horn]                              ::  /;  operate on
      [%see p=beam q=horn]                              ::  /:  relative to
      [%sic p=tile q=horn]                              ::  /^  cast
      [%toy p=mark]                                     ::  /mark/  static
  ==                                                    ::
++  silk                                                ::  construction layer
  $&  [p=silk q=silk]                                   ::  cons
  $%  [%bake p=mark q=beam r=path]                      ::  local synthesis
      [%boil p=mark q=beam r=path]                      ::  general synthesis
      [%call p=silk q=silk]                             ::  slam
      [%cast p=mark q=silk]                             ::  translate
      [%done p=(set beam) q=cage]                       ::  literal
      [%dude p=tank q=silk]                             ::  error wrap
      [%dune p=(set beam) q=(unit cage)]                ::  unit literal
      [%mute p=silk q=(list (pair wing silk))]          ::  mutant
      [%plan p=beam q=spur r=hood]                      ::  structured build
      [%reef ~]                                         ::  kernel reef
      [%ride p=twig q=silk]                             ::  silk thru twig
      [%vale p=mark q=ship r=*]                         ::  validate [our his]
  ==                                                    ::
++  volt  ?(%low %high)                                 ::  voltage
++  torc  $|(?(%iron %gold) [%lead p=ship])             ::  security control
++  roon                                                ::  reverse ames msg
  $%  [%d p=mark q=*]                                   ::  diff (rush)
      [%x ~]                                            ::
  ==                                                    ::
++  rook                                                ::  forward ames msg
  $%  [%m p=mark q=*]                                   ::  message
      [%s p=path]                                       ::  subscribe
      [%u ~]                                            ::  cancel/unsubscribe
  ==                                                    ::
--                                                      ::
|%  ::::::::::::::::::::::::::::::::::::::::::::::::::::::    local arvo
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::
++  club                                                ::  agent action
  $%  [%peer p=path]                                    ::  subscribe
      [%poke p=cage]                                    ::  apply
      [%pull ~]                                         ::  unsubscribe
      [%pump p=(unit tang)]                             ::  pump yes/no
  ==                                                    ::
++  culm                                                ::  config action
  $%  [%load p=scup]                                    ::  load/reload
  ::  [%kick ~]                                         ::  restart everything
  ::  [%stop ~]                                         ::  toggle suspend
  ::  [%wipe ~]                                         ::  destroy all state
  ==                                                    ::
++  cuss  (pair dude club)                              ::  internal kiss
++  cuft                                                ::  internal gift
  $%  [%coup p=(unit tang)]                             ::  poke result
      [%quit ~]                                         ::  close subscription
      [%reap p=(unit tang)]                             ::  peer result
      [%rush p=cage]                                    ::  subscription output
  ==                                                    ::
++  cote                                                ::  ++ap note
  $%  [%meta p=@tas q=vase]                             ::  
      [%send p=ship q=cuss]                             ::  
  ==                                                    ::
++  cove  (pair duct (mold cote cuft))                  ::  internal move 
++  dude  term                                          ::  local identity
++  scup  (pair ship desk)                              ::  autoupdate
++  tang  (list tank)                                   ::  error abstraction
--                                                      ::
|%  ::::::::::::::::::::::::::::::::::::::::::::::::::::::    %behn cards
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::
++  kiss-ames                                           ::
  $%  [%wont p=sock q=path r=*]                         ::
  ==                                                    ::
++  kiss-behn                                           ::  incoming request
  $%  [%conf p=ship q=dude r=culm]                      ::  configure app
      [%deal p=sock q=cuss]                             ::  full transmission
      [%init p=ship]                                    ::  set owner
      [%rote p=sack q=path r=*]                         ::  remote request
      [%roth p=sack q=path r=*]                         ::  remote response
  ==                                                    ::
++  kiss-ford                                           ::
  $%  [%exec p=@p q=(unit silk)]                        ::  make / kill
      [%wasp p=@p q=@uvH]                               ::  depends query
  ==                                                    ::
++  gift-ames                                           ::
  $%  [%woot p=ship q=(unit ares)]                      ::  e2e reaction message
  ==                                                    ::
++  gift-behn                                           ::  outgoing result
  $%  [%onto p=(unit tang)]                             ::  about agent
      [%unto p=cuft]                                    ::  within agent
  ==                                                    ::
++  gift-ford                                           ::  out result <-$
  $%  [%made p=@uvH q=(each cage tang)]                 ::  computed result
      [%news ~]                                         ::  fresh depends       
  ==                                                    ::
++  sign-behn                                           ::  incoming result
  $%  [%a gift-ames]
      [%b gift-behn]                                    ::
      [%f gift-ford]                                    ::
  ==                                                    ::
++  note-behn                                           ::  outgoing request
  $?  [@tas %meta p=vase]                               ::
  $%  [%a kiss-ames]                                    ::
      [%b kiss-behn]                                    ::
      [%f kiss-ford]                                    ::
  ==  ==                                                ::
++  move  ,[p=duct q=(mold note-behn gift-behn)]        ::  typed move
--                                                      ::
|%  ::::::::::::::::::::::::::::::::::::::::::::::::::::::    %behn state
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::
++  axle                                                ::  all state
  $:  %0                                                ::  state version
      pol=(map ship mast)                               ::  apps by ship
  ==                                                    ::
++  gest                                                ::  subscriber data
  $:  sup=(map bone (pair ship path))                   ::  subscribers
      pus=(jug path bone)                               ::  srebircsbus
      qel=(map bone ,@ud)                               ::  queue meter
  ==                                                    ::
++  mast                                                ::  ship state
  $:  sys=duct                                          ::  system duct
      sap=(map ship scad)                               ::  foreign contacts
      bum=(map dude seat)                               ::  running agents
      wub=(map dude sofa)                               ::  waiting queue
  ==                                                    ::
++  ffuc                                                ::  new cuff
    $:  p=(unit (set ship))                             ::  disclosing to
        q=ship                                          ::  attributed to
    ==                                                  ::
++  prey  (pair volt ffuc)                              ::  privilege
++  scad                                                ::  foreign connection
  $:  p=@ud                                             ::  index
      q=(map duct ,@ud)                                 ::  by duct
      r=(map ,@ud duct)                                 ::  by index
  ==                                                    ::
++  scar                                                ::  opaque input
  $:  p=@ud                                             ::  bone sequence
      q=(map duct bone)                                 ::  by duct
      r=(map bone duct)                                 ::  by bone
  ==                                                    ::
++  seat                                                ::  agent state
  $:  mom=duct                                          ::  control duct 
      liv=?                                             ::  unstopped
      toc=torc                                          ::  privilege
      tyc=stic                                          ::  statistics
      ged=gest                                          ::  subscribers
      hav=vase                                          ::  running state
      pup=scup                                          ::  update control
      zam=scar                                          ::  opaque ducts
  ==                                                    ::
++  sofa  (qeu (pair duct kiss-behn))                   ::  waiting kisses
++  stic                                                ::  statistics
  $:  act=@ud                                           ::  change number
      eny=@uvI                                          ::  entropy
      lat=@da                                           ::  time
  ==                                                    ::
--                                                      ::
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::  vane header
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::
.  ==
=|  all=axle                                            ::  all vane state
|=  $:  now=@da                                         ::  urban time
        eny=@uvI                                        ::  entropy
        ska=sled                                        ::  activate
    ==                                                  ::  opaque core
|%  ::::::::::::::::::::::::::::::::::::::::::::::::::::::  state machine
    ::::::::::::::::::::::::::::::::::::::::::::::::::::::
++  mo
  |_  $:  $:  our=@p 
              hen=duct
              moz=(list move)
          ==
          mast
      ==
  ++  mo-abed                                           ::  initialize
    |=  [our=@p hen=duct]
    ^+  +>
    %_    +>
      our  our
      hen  hen
      +<+  (~(got by pol.all) our)
    ==
  ::
  ++  mo-abet                                           ::  resolve to 
    ^-  [(list move) _+>+]
    :_  +>+(pol.all (~(put by pol.all) our +<+))
    %-  flop
    %+  turn  moz
    |=  a=move
    ?.  ?=(%pass -.q.a)  a
    [p.a %pass [(scot %p our) p.q.a] q.q.a]
  ::
  ++  mo-conf                                           ::  configure
    |=  [dap=dude lum=culm]
    (mo-boot dap ?:((~(has by bum) dap) %old %new) p.lum)
  ::
  ++  mo-pass                                           ::  standard pass
    |=  [pax=path noh=note-behn]
    %_(+> moz :_(moz [hen %pass pax noh]))
  ::
  ++  mo-give
    |=  git=gift-behn
    %_(+> moz :_(moz [hen %give git]))
  ::
  ++  mo-boom                                           ::  complete new boot
    |=  [dap=dude pup=scup dep=@uvH cux=(each cage tang)]
    ^+  +>
    ?-    -.cux
        %|  ~&  [%mo-boom-lame p.cux]
            (mo-give %onto `p.cux)
        %&
      =.  +>  (mo-give %onto ~)
      =.  +>  (mo-bold dap pup dep)
      =.  +>  (mo-born dap pup q.p.cux)
      ap-abet:(ap-prep:(ap-abed:ap dap [%high [~ our]]) ~)
    ==
  ::
  ++  mo-born                                           ::  new seat
    |=  [dap=dude pup=scup hav=vase]
    =+  sat=*seat
    %_    +>.$
        bum
      %+  ~(put by bum)  dap
      %_  sat
        mom  hen
        pup  pup
        hav  hav
        p.zam  1
        q.zam  [[~ 0] ~ ~]
        r.zam  [[0 ~] ~ ~]
      ==
    ==
  ::
  ++  mo-boon                                           ::  complete old boot
    |=  [dap=dude pup=scup dep=@uvH cux=(each cage tang)]
    ^+  +>
    ~&  [%mo-boon dap pup]
    ?.  (~(has by bum) dap)  
      ~&  [%behn-old-boon dap]
      +>
    =.  +>  (mo-bold dap pup dep)
    ?-  -.cux
      %|  ~&  [%mo-boon-lame p.cux]
          (mo-give %onto `p.cux)
      %&  ~&  %mo-boon-prep
          ap-abet:(ap-prep:(ap-abed:ap dap [%high [~ our]]) `q.p.cux)
    ==
  ::
  ++  mo-bold                                           ::  wait for dep
    |=  [dap=dude pup=scup dep=@uvH]
    ^+  +>
    %+  mo-pass  [%sys %old dap (scot %p p.pup) q.pup ~] 
    [%f %wasp our dep]
  ::
  ++  mo-boot                                           ::  create ship
    |=  [dap=dude how=?(%new %old) pup=scup]
    ^+  +>
    ~&  [%mo-boot dap how pup]
    %+  mo-pass  [%sys how dap (scot %p p.pup) q.pup ~]
    ^-  note-behn
    [%f %exec our `[%boil %core [[p.pup q.pup [%da now]] [dap %zap ~]] ~]]
  ::
  ++  mo-away                                           ::  foreign request
    |=  [him=ship caz=cuss]                             ::  
    ^+  +>
    ~&  [%mo-away him caz]
    ?<  ?=(%pump -.q.caz)
    =^  num  +>.$  (mo-bale him)
    =+  ^=  roc  ^-  rook
        ?-  -.q.caz
          %poke  [%m p.p.q.caz q.q.p.q.caz]
          %pull  [%u ~]
          %peer  [%s p.q.caz]
        ==
    %+  mo-pass  
      [%sys %way -.q.caz ~]
    `note-behn`[%a %wont [him our] [%q %be p.caz ~] [num roc]]
  ::
  ++  mo-baal                                           ::  error convert a
    |=  art=(unit ares)
    ^-  ares
    ?~(art ~ ?~(u.art `[%blank ~] u.art))
  ::
  ++  mo-baba                                           ::  error convert b
    |=  ars=ares
    ^-  (unit tang)
    ?~  ars  ~
    `[[%leaf (trip p.u.ars)] q.u.ars]
  ::
  ++  mo-awed                                           ::  foreign response
    |=  [him=ship why=?(%peer %poke %pull) art=(unit ares)]
    ^+  +>
    ~&  [%mo-awed why art]
    =+  tug=(mo-baba (mo-baal art))
    ?-  why
      %peer  (mo-give %unto %reap tug)
      %poke  (mo-give %unto %coup tug)
      %pull  ~&  [%pull-fail tug]
             +>.$
    ==
  ::
  ++  mo-bale                                           ::  assign outbone
    |=  him=ship 
    ^-  [@ud _+>]
    =+  sad=(fall (~(get by sap) him) `scad`[1 ~ ~])
    =+  nom=(~(get by q.sad) hen)
    ?^  nom  [u.nom +>.$]
    :-  p.sad
    %_    +>.$
        sap
      %+  ~(put by sap)  him
      %_  sad
        p  +(p.sad)
        q  (~(put by q.sad) hen p.sad)
        r  (~(put by r.sad) p.sad hen)
      ==
    ==
  ::
  ++  mo-ball                                           ::  find outbone
    |=  [him=ship num=@ud]
    ^-  duct
    (~(got by r:(~(got by sap) him)) num)
  ::
  ++  mo-come                                           ::  handle locally
    |=  [her=ship caz=cuss]
    ^+  +>
    =+  pry=`prey`[%high [~ her]]
    (mo-club p.caz pry q.caz)
  ::
  ++  mo-coup                                           ::  back from mo-away
    |=  [dap=dude him=ship cup=ares]
    %^  mo-give  %unto  %coup
    ?~  cup  ~
    [~ `tang`[[%leaf (trip p.u.cup)] q.u.cup]]
  ::
  ++  mo-cyst                                           ::  take in /sys
    |=  [pax=path sih=sign-behn]
    ^+  +>
    ?+    -.pax  !!
        %old                                            ::  reload old
      ?>  ?=([%f %made *] sih)
      ?>  ?=([@ @ @ ~] t.pax)
      (mo-boon i.t.pax [(slav %p i.t.t.pax) i.t.t.t.pax] +>.sih)
    ::
        %way                                            ::  outbound request
      ?>  ?=([%a %woot *] sih)
      ?>  ?=([@ @ ~] t.pax)
      ?>  =(p.+>.sih (slav %p i.t.pax))
      %-  mo-awed
      :*  p.+>.sih
          (?(%peer %poke %pull) i.t.pax)
          +>+.sih
      ==
    ::
        %rep                                            ::  reverse request
      ?>  ?=([@ @ @ ~] t.pax)
      ?>  ?=([%f %made *] sih)
      =+  :*  him=(slav %p i.t.pax)
              dap=i.t.t.pax
              num=(slav %ud i.t.t.t.pax)
          ==
      ?-  -.q.+>.sih
        %|  ~&  [%rush-crash p.q.+>.sih]                ::  crash is correct
            !!
        %&  (mo-give(hen (mo-ball him num)) %unto %rush `cage`p.q.+>.sih)
      ==
    ::
        %req                                            ::  inbound request
      ?>  ?=([@ @ @ ~] t.pax)
      =+  :*  him=(slav %p i.t.pax)
              dap=i.t.t.pax
              num=(slav %ud i.t.t.t.pax)
          ==
      ?:  ?=([%f %made *] sih)
        ?-  -.q.+>.sih
          %|  (mo-give %unto %coup `p.q.+>.sih)         ::  XX should crash
          %&  (mo-pass pax %b %deal [him our] i.t.t.pax %poke p.q.+>.sih)
        ==
      ?:  ?=([%a %woot *] sih)
        ?~  q.+>.sih  +>.$
        ~&  [%behn-woot-lost pax +>.sih]
        +>.$
      ?>  ?=([%b %unto *] sih)
      =+  cuf=`cuft`+>.sih
      ?-    -.cuf
        %coup  (mo-give %unto %coup p.cuf)
        %rush  %+  mo-pass  pax
               [%a %wont [him our] [%q %bh dap ~] [num %d p.p.cuf q.q.p.cuf]]
        %quit  %+  mo-pass  pax
               [%a %wont [him our] [%q %bh dap ~] [num %x ~]]
        %reap  (mo-give %unto %reap p.cuf)
      ==
    ::
        %new
      ?>  ?=([%f %made *] sih)
      ?>  ?=([@ @ @ ~] t.pax)
      (mo-boom i.t.pax [(slav %p i.t.t.pax) i.t.t.t.pax] +>.sih)
    ::
        %way
      ?>  ?=([%a %woot *] sih)
      ?>  ?=([@ @ ~] t.pax)
      =+  suz=`(unit ares)`+>+.sih
      %^    mo-coup
          i.t.t.pax
        (slav %p i.t.pax)
      ?~(suz ~ ?~(u.suz `[%woot-no-way ~] u.suz))
    ==
  ::
  ++  mo-cook                                           ::  take in /use
    |=  [pax=path hin=(hypo sign-behn)]
    ^+  +>
    ?>  ?=([@ @ ?(%inn %out) *] pax)
    =+  dap=`@tas`i.pax
    =+  pry=`prey`[%high [~ (slav %p i.t.pax)]]
    =+  pap=(ap-abed:ap i.t.pax pry) 
    =<  ap-abet
    ?-  i.t.t.pax
      %inn  (ap-pour:pap t.t.t.pax `vase`hin)
      %out  (ap-pout:pap t.t.t.pax `vase`hin)
    ==
  ::
  ++  mo-club                                           ::  local action
    |=  [dap=dude pry=prey cub=club]
    ap-abet:(ap-club:(ap-abed:ap dap pry) cub)
  ::
  ++  mo-gawk                                           ::  ames forward
    |=  [him=@p dap=dude num=@ud rok=rook]
    %+  mo-pass  
      [%sys %req (scot %p him) dap (scot %ud num) ~]
    ^-  note-behn
    ?-  -.rok
      %m  [%f %exec our ~ %vale p.rok our q.rok]        ::  XX use build beak
      %s  [%b %deal [him our] dap %peer p.rok]
      %u  [%b %deal [him our] dap %pull ~]
    ==
  ::
  ++  mo-gawd                                           ::  ames backward
    |=  [him=@p dap=dude num=@ud ron=roon]
    ?-    -.ron
        %d
      %+  mo-pass  
        [%sys %rep (scot %p him) dap (scot %ud num) ~]
      [%f %exec our ~ %vale p.ron our q.ron]
    ::
        %x  (mo-give %unto %quit ~)
    ==
  ::
  ++  ap
    |_  $:  $:  dap=dude
                pry=prey
                ost=bone
                zip=(list cove)
            ==
            seat
        ==
    ::
    ++  ap-abed                                         ::  initialize
      |=  [dap=dude pry=prey]
      ^+  +>
      =:  ^dap  dap
          ^pry  pry
        ==
      =+  unt=(~(get by q.zam) hen)
      ?^  unt
        +>.$(ost u.unt)
      %=  +>.$
        ost  p.zam
        p.zam  +(p.zam)
        q.zam  (~(put by q.zam) hen p.zam)
        r.zam  (~(put by r.zam) p.zam hen)
        +<+    `seat`(~(got by bum) dap)
      ==
    ::
    ++  ap-abet                                         ::  resolve
      ^+  +>
      %_  +>  
        bum  (~(put by bum) dap +<+)
        moz  (weld (turn zip ap-aver) moz)
      ==
    ::
    ++  ap-aver                                         ::  cove to move
      |=  cov=cove
      ^-  move 
      :-  p.cov
      ?-    -.q.cov
          ?(%slip %sick)  !!
          %give  [%give %unto p.q.cov]
          %pass
        :+  %pass  `path`[%use dap p.q.cov]
        ?-  -.q.q.cov
          %send  `note-behn`[%b %deal [our p.q.q.cov] q.q.q.cov]
          %meta  `note-behn`[`@tas`p.q.q.cov %meta `vase`q.q.q.cov]
        ==
      ==
    ::
    ++  ap-club                                         ::  apply effect
      |=  cub=club
      ^+  +>
      ?-  -.cub
        %poke   (ap-poke +.cub)
        %peer   (ap-peer +.cub)
        %pull   ap-pull
        %pump   ?^(p.cub ~&([%club-fall p.cub] +>.$) ap-fall)
      ==
    ::
    ++  ap-find                                         ::  general arm
      |=  [cog=term pax=path]
      =+  dep=0
      |-  ^-  (unit (pair ,@ud term))
      =+  ^=  spu
          ?~  pax  ~ 
          $(pax t.pax, dep +(dep), cog (ap-hype cog i.pax))
      ?^  spu  spu
      ?:((ap-fond cog) ~ `[dep cog])
    ::
    ++  ap-fall                                         ::  drop from queue
      ^+  .
      =+  soy=(~(get by qel.ged) ost)
      ?~  soy  ~&(%ap-fall-underflow +)
      ?:  =(0 u.soy)  
        +(qel.ged (~(del by qel.ged) ost))
      +(qel.ged (~(put by qel.ged) ost (dec u.soy)))
    ::
    ++  ap-fill                                         ::  add to queue
      ^-  [? _.]
      =+  suy=(fall (~(get by qel.ged) ost) 0)
      ?:  =(10 suy)  [%| +]
      [%& +(qel.ged (~(put by qel.ged) ost +(suy)))]
    ::
    ++  ap-pull                                         ::  pull subscription
      ^+  .
      ~&  %ap-pull
      !!
    ::
    ++  ap-fond                                         ::  check for arm
      |=  cog=term
      ^-  ?
      (slab cog p.hav)
    ::
    ++  ap-hype                                         ::  hyphenate
      |=([a=term b=term] `term`(cat 3 a (cat 3 '-' b)))
    ::
    ++  ap-give                                         ::  return result
      |=  cit=cuft
      ^+  +>
      +>(zip :_(zip [(~(got by r.zam) ost) %give cit]))
    ::
    ++  ap-pass                                         ::  request action
      |=  [pax=path coh=cote]
      ^+  +>
      +>(zip :_(zip [(~(got by r.zam) ost) %pass pax coh]))
    ::
    ++  ap-call                                         ::  call into agent
      |=  [cog=term arg=vase]
      ^-  [(unit tang) _+>]
      =+  puz=(mule |.((~(mint ut p.hav) [%noun [%cnzy cog]])))
      ?:  ?=(%| -.puz)  [`p.puz +>.$]
      =+  wiz=(mule |.((slit p.p.puz p.arg)))
      ?:  ?=(%| -.wiz)  [`p.wiz +>.$]
      =-  ?-  -.zep
            %|  [`p.zep +>.$]
            %&  (ap-sake p.wiz +.zep)
          ==
      ^=  zep  ^-  (each noun tang)
      =+  ton=(mock [q.hav q.p.puz] |=(* ~))
      ?-  -.ton
        %0  =+  ton=(mong [p.ton q.arg] |=(* ~))
            ?-  -.ton
              %0  [%& p.ton]
              %1  [%| (turn p.ton |=(a=* (smyt (path a))))]
              %2  [%| p.ton]
              ==
        %1  [%| (turn p.ton |=(a=* (smyt (path a))))]
        %2  [%| p.ton]
      ==
    ::
    ++  ap-prep                                         ::  call to install
      |=  vux=(unit vase)
      ^+  +>
      =^  tur  +>.$
          %+  ap-call  %prep
          ;:  slop
            !>(`@ud`ost)
            !>(`@p`q.q.pry)
            ?~  vux  !>(~)
            (slop !>(~) u.vux)
          ==
      ?~  tur  
        +>.$
      +>.$
    ::
    ++  ap-suck                                         ::  standard tang
      |=  msg=tape
      ^-  tang
      [%leaf (weld "behn: {<dap>}:" msg)]~
    ::
    ++  ap-safe                                         ::  process move list
      |=  vax=vase
      ^-  (each (list cove) tang)
      ?~  q.vax  [%& ~]
      =+  sud=(ap-move (slot 2 vax))
      ?:  ?=(%| -.sud)  sud
      =+  res=$(vax (slot 3 vax))
      ?:  ?=(%| -.res)  res
      [%& p.sud p.res]
    ::
    ++  ap-move                                         ::  process each move
      |=  vax=vase
      ^-  (each cove tang)
      ?@  q.vax    [%| (ap-suck "move: invalid move (atom)")]
      ?^  -.q.vax  [%| (ap-suck "move: invalid move (bone)")]
      ?@  +.q.vax  [%| (ap-suck "move: invalid move (card)")]
      =+  hun=(~(get by r.zam) -.q.vax)
      ?~  hun  [%| (ap-suck "move: invalid card (bone)")]
      =+  cav=(slot 7 vax)
      ?+  +<.q.vax  [%| (ap-suck "move: invalid card (stem)")]
        %rush  (ap-move-rush u.hun cav)
        %send  (ap-move-send u.hun cav)
        %pass  (ap-move-pass u.hun cav)
        %quit  (ap-move-quit u.hun cav)
      ==
    ::
    ++  ap-move-quit                                    ::  give quit move
      |=  [neh=duct vax=vase]
      ^-  (each cove tang)
      ?^  q.vax  [%| (ap-suck "move: improper quit")]
      [%& `cove`[neh %give `cuft`[%quit ~]]]
    ::
    ++  ap-term                                         ::  atomic vase
      |=  [a=@tas b=@]  
      ^-  vase
      [[%cube b %atom a] a]
    ::
    ++  ap-move-rush                                    ::  give rush move
      |=  [neh=duct vax=vase]
      ^-  (each cove tang)
      ?.  &(?=(^ q.vax) ?=(@ -.q.vax) ((sane %tas) -.q.vax))
        [%| (ap-suck "move: improper rush")]
      [%& neh %give %rush `cage`[-.q.vax vax]]
    ::
    ++  ap-move-send                                    ::  pass gall action
      |=  [neh=duct vax=vase]
      ^-  (each cove tang)
          ::        2 3  6 7 14 15 30  31
      ?.  ?&  ?=([p=* [q=@ [r=@ [s=@ t=*]]]] q.vax)
              (gte 1 (met 7 q.q.vax))
              ((sane %tas) r.q.vax)
          ==
        [%| (ap-suck "move: malformed send")]
      =+  pux=((soft path) p.q.vax)
      ?.  &(?=(^ pux) (levy u.pux (sane %ta)))
        [%| (ap-suck "move: malformed path")]
      ?:  ?=(%poke s.q.vax)
        ?.  ?&  ?=([p=@ q=*] t.q.vax)
                ((sane %tas) p.t.q.vax)
            ==
          [%| (ap-suck "move: malformed poke")]
        :^  %&  neh  %pass
        :-  [(scot %p q.q.vax) %out r.q.vax u.pux]
        ^-  cote
        [%send q.q.vax r.q.vax %poke p.t.q.vax (slot 31 vax)]
      =+  cob=((soft club) [s t]:q.vax)
      ?~  cob
        [%| (ap-suck "move: malformed club")]
      :^  %&  neh  %pass
      :-  [(scot %p q.q.vax) %out r.q.vax u.pux]
      [%send q.q.vax r.q.vax u.cob]
    ::
    ++  ap-vain                                         ::  card to vane
      |=  sep=@tas
      ^-  (unit ,@tas)
      ?+  sep  ~&  [%ap-vain sep]
               ~
        %exec  `%f
        ::  etc
      ==
    ::
    ++  ap-move-pass                                    ::  pass general move 
      |=  [neh=duct vax=vase]
      ^-  (each cove tang)
      ?.  &(?=([* @ *] q.vax) ((sane %tas) +<.q.vax))
        [%| (ap-suck "move: malformed pass")]
      =+  pux=((soft path) -.q.vax)
      ?.  &(?=(^ pux) (levy u.pux (sane %ta)))
        [%| (ap-suck "move: malformed path")]
      =+  huj=(ap-vain +<.q.vax)
      ?~  huj
        [%| (ap-suck "move: unknown note {(trip +<.q.vax)}")]
      :^  %&  neh  %pass 
      :-  [(scot %p q.q.pry) %inn u.pux]
      [%meta u.huj (slot 7 vax)]
    ::
    ++  ap-save                                         ::  verify core
      |=  vax=vase
      ^-  (each vase tang)
      ?:  (~(nest ut p.hav) %| p.vax)
        [%| (ap-suck "bad core")]
      [%& vax]
    ::
    ++  ap-sake                                         ::  handle result
      |=  vax=vase
      ^-  [(unit tang) _+>]
      ?:  ?=(@ q.vax)
        [`(ap-suck "sake: invalid product (atom)") +>.$]
      =+  muz=(ap-safe (slot 2 vax))
      ?:  ?=(%| -.muz)  [`p.muz +>.$]
      =+  sav=(ap-save (slot 3 vax))
      ?:  ?=(%| -.sav)  [`p.sav +>.$]
      :-  ~
      %_  +>.$
        zip  (weld (flop p.muz) zip)
        hav  p.sav 
      ==
    ::
    ++  ap-poke                                         ::  apply %poke
      |=  cag=cage
      ^+  +>
      =+  cug=(ap-find %poke p.cag ~)
      ?~  cug
        (ap-give %coup `(ap-suck "no poke arm"))
      =^  tur  +>.$
          %+  ap-call  q.u.cug
          ;:  slop
            !>(`@ud`ost)
            !>(`@p`q.q.pry)
            ?.  =(0 p.u.cug)  q.cag
            (slop (ap-term %tas p.cag) q.cag)
          ==
      (ap-give %coup tur)
    ::
    ++  ap-peon                                         ::  add subscriber
      |=  pax=path
      %_  +>.$
        sup.ged  (~(put by sup.ged) ost [q.q.pry pax])
        pus.ged  (~(put ju pus.ged) pax ost)
      ==
    ::
    ++  ap-peer                                         ::  apply %peer
      |=  pax=path
      ^+  +>
      =+  cug=(ap-find %peer pax)
      ?~  cug
        (ap-peon pax)
      =^  cam  +>.$ 
          %+  ap-call  q.u.cug
          !>([`@ud`ost `@p`q.q.pry `path`(slag p.u.cug pax)])
      ?^  cam
        (ap-give %reap cam)
      (ap-give:(ap-peon pax) %reap ~)
    ::
    ++  ap-pour-fail                                    ::  pour error
      |=  [wut=@tas why=tang]
      ^+  +>
      ~&  [%ap-pour-fail wut why]
      +>   
    ::
    ++  ap-pour                                         ::  generic take
      |=  [pax=path vax=vase]
      ^+  +>
      ?.  &(?=([@ *] q.vax) ((sane %tas) -.q.vax))
        (ap-pour-fail %pour (ap-suck "pour: malformed card"))
      =+  cug=(ap-find [%pour -.q.vax pax])
      ?~  cug
        (ap-pour-fail -.q.vax (ap-suck "pour: no (trip -.q.vax)}: {<pax>}"))
      =^  cam  +>.$
          %+  ap-call  q.u.cug
          ;:  slop
            !>(`@ud`ost)
            !>(`@p`q.q.pry)
            ?:  =(0 p.u.cug)
              (slop !>(`path`pax) !>(vax))
            (slop !>(`path`(slag (dec p.u.cug) pax)) (slot 3 vax))
          ==
      ?^  cam  (ap-pour-fail -.q.vax u.cam)
      +>.$
    ::
    ++  ap-pout                                         ::  specific take
      |=  [pax=path vax=vase]
      ^+  +>
      ?>  &(?=([@ *] pax) ((sane %tas) i.pax))
      ?>  &(?=([@ *] q.vax) ((sane %tas) -.q.vax))
      =+  ape=i.pax 
      ?:  ?=(%rush -.q.vax)
        ?>  &(?=([@ *] +.q.vax) ((sane %tas) +<.q.vax))
        (ap-rush q.q.pry ape t.pax [+<.q.vax (slot 7 vax)])
      (ap-punk q.q.pry ape t.pax -.q.vax (slot 3 vax))
    ::
    ++  ap-punk                                         ::  non-rush gall take
      |=  [her=ship ape=term pax=path wut=term vax=vase]
      ^+  +>
      =+  cug=(ap-find [wut pax])
      ?~  cug
        (ap-pour-fail wut (ap-suck "pour: no punk"))
      =^  cam  +>.$  (ap-call q.u.cug :(slop !>(`@ud`ost) !>(`@p`q.q.pry) vax))
      ?^  cam  (ap-pour-fail q.u.cug u.cam)
      +>.$
    ::
    ++  ap-rush                                         ::  pour a rush
      |=  [her=ship ape=term pax=path cag=cage]
      =+  cug=(ap-find [%rush p.cag pax])
      ?~  cug
        (ap-pour-fail %rush (ap-suck "pour: no rush"))
      =^  cam  +>.$
          %+  ap-call  q.u.cug
          ;:  slop
            !>(`@p`q.q.pry)
            !>(`@ud`ost)
            ?:  =(0 p.u.cug)
              :(slop !>(`path`pax) !>(cag))
            :(slop !>(`path`(slag (dec p.u.cug) pax)) q.cag)
          ==
      ?^  cam  (ap-pour-fail q.u.cug u.cam)
      +>.$
    --
  --
++  call                                                ::  request
  |=  [hen=duct hic=(hypo (hobo kiss-behn))]
  ^-  [p=(list move) q=_..^$]
  =>  .(q.hic ?.(?=(%soft -.q.hic) q.hic ((hard kiss-behn) p.q.hic)))
  ~&  [%behn-call q.hic]
  ?-    -.q.hic
      %conf
    ?.  (~(has by pol.all) p.q.hic)
      ~&  [%behn-not-ours p.q.hic]
      [~ ..^$]
    mo-abet:(mo-conf:(mo-abed:mo p.q.hic hen) q.q.hic r.q.hic)
  ::
      %deal
    =<  mo-abet
    ?.  (~(has in pol.all) q.p.q.hic)                   ::  either to us
      ?>  (~(has in pol.all) p.p.q.hic)                 ::  or from us
      (mo-away:(mo-abed:mo p.p.q.hic hen) q.p.q.hic q.q.hic)
    (mo-come:(mo-abed:mo q.p.q.hic hen) p.p.q.hic q.q.hic)
  ::
      %init 
    ~&  [%behn-init p.q.hic]
    [~ ..^$(pol.all (~(put by pol.all) p.q.hic [hen ~ ~ ~]))]
  ::
      %rote
    ~&  [%behn-rote p.q.hic]
    ?.  (~(has by pol.all) p.p.q.hic)
      ~&  [%behn-not-ours p.q.hic]
      [~ ..^$]
    ?>  ?=([@ ~] q.q.hic)
    =+  dap=i.q.q.hic
    =+  our=p.p.q.hic
    =+  him=q.p.q.hic
    =+  mes=((hard ,[@ud rook]) r.q.hic)
    =<  mo-abet
    (mo-gawk:(mo-abed:mo our hen) him dap mes)
  ::
      %roth
    ~&  [%behn-roth p.q.hic]
    ?.  (~(has by pol.all) p.p.q.hic)
      ~&  [%behn-not-ours p.q.hic]
      [~ ..^$]
    ?>  ?=([@ ~] q.q.hic)
    =+  dap=i.q.q.hic
    =+  our=p.p.q.hic
    =+  him=q.p.q.hic
    =+  mes=((hard ,[@ud roon]) r.q.hic)
    =<  mo-abet
    (mo-gawd:(mo-abed:mo our hen) him dap mes)
  ==
::
++  doze                                                ::  sleep until
  |=  [now=@da hen=duct]
  ^-  (unit ,@da)
  ~
::
++  load                                                ::  recreate vane
  |=  old=axle
  ^+  ..^$
  ..^$(all old)
::
++  scry
  |=  [fur=(unit (set monk)) ren=@tas who=ship syd=desk lot=coin tyl=path]
  ^-  (unit (unit (pair mark ,*)))
  [~ ~]
::
++  stay                                                ::  save w/o cache
  `axle`all
::
++  take                                                ::  response
  |=  [tea=wire hen=duct hin=(hypo sign-behn)]
  ^-  [p=(list move) q=_..^$]
  ~|  [%behn-take tea]
  ?>  ?=([@ ?(%sys %use) *] tea) 
  =+  our=(need (slaw %p i.tea))
  =+  mow=(mo-abed:mo our hen)
  ?:  ?=(%sys i.t.tea)
    mo-abet:(mo-cyst:mow t.t.tea q.hin)
  mo-abet:(mo-cook:mow t.tea hin)
--
