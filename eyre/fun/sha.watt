|=  raw=@  ^-  0x0*
~|  %sha
=+  [few==>(fe .(a 5)) wac=|=([a=@ b=@] (cut 5 [a 1] b))]
=+  [sum=sum.few ror=ror.few net=net.few inv=inv.few]
=+  ral=(met 0 raw)
=+  ^=  ful
    %+  can  0
    :~  [ral raw]
        [1 1]
        [(~(dif fe 9) 448 +(ral)) 0]
        [64 (~(net fe 6) ral)]
    ==
=+  lex=(met 9 ful)
=+  ^=  kbx  0xc67178f2\ /bef9a3f7\ /a4506ceb\ /90befffa\ 
              /8cc70208\ /84c87814\ /78a5636f\ /748f82ee\ 
              /682e6ff3\ /5b9cca4f\ /4ed8aa4a\ /391c0cb3\ 
              /34b0bcb5\ /2748774c\ /1e376c08\ /19a4c116\ 
              /106aa070\ /f40e3585\ /d6990624\ /d192e819\ 
              /c76c51a3\ /c24b8b70\ /a81a664b\ /a2bfe8a1\ 
              /92722c85\ /81c2c92e\ /766a0abb\ /650a7354\ 
              /53380d13\ /4d2c6dfc\ /2e1b2138\ /27b70a85\ 
              /14292967\ /06ca6351\ /d5a79147\ /c6e00bf3\ 
              /bf597fc7\ /b00327c8\ /a831c66d\ /983e5152\ 
              /76f988da\ /5cb0a9dc\ /4a7484aa\ /2de92c6f\ 
              /240ca1cc\ /0fc19dc6\ /efbe4786\ /e49b69c1\ 
              /c19bf174\ /9bdc06a7\ /80deb1fe\ /72be5d74\ 
              /550c7dc3\ /243185be\ /12835b01\ /d807aa98\ 
              /ab1c5ed5\ /923f82a4\ /59f111f1\ /3956c25b\ 
              /e9b5dba5\ /b5c0fbcf\ /71374491\ /428a2f98
=+  ^=  hax  0x5be0cd19\ /1f83d9ab\ /9b05688c\ /510e527f\
              /a54ff53a\ /3c6ef372\ /bb67ae85\ /6a09e667*
=+  piq=0*
|-  ^-  @
?:  =(piq lex)
  (rep 5 (turn (rip 5 hax) net))
=+  ^=  wox
    =+  dux=(cut 9 [piq 1] ful)
    =+  wox=(rep 5 (turn (rip 5 dux) net))
    =+  i=16*
    |-  ^-  @
    ?:  =(64 i)
      wox  
    =+  :*  l=(wac (sub i 15) wox)
            m=(wac (sub i 2) wox)
            n=(wac (sub i 16) wox)
            o=(wac (sub i 7) wox)
        ==
    =+  x=:(mix (ror 7 l) (ror 18 l) (rsh 0 3 l))
    =+  y=:(mix (ror 17 m) (ror 19 m) (rsh 0 10 m))
    =+  z=:(sum n x o y)
    $(wox (con (lsh 5 i z) wox), i +(i))
=+  i=0*
=+  :*  a=(wac 0 hax)
        b=(wac 1 hax)
        c=(wac 2 hax)
        d=(wac 3 hax)
        e=(wac 4 hax)
        f=(wac 5 hax)
        g=(wac 6 hax)
        h=(wac 7 hax)
    ==
|-  ^-  @
?:  =(64 i)
  %=  $$
    piq  +(piq)
    hax  %+  rep  5
         :~  (sum a (wac 0 hax))
             (sum b (wac 1 hax))
             (sum c (wac 2 hax))
             (sum d (wac 3 hax))
             (sum e (wac 4 hax))
             (sum f (wac 5 hax))
             (sum g (wac 6 hax))
             (sum h (wac 7 hax))
         ==
  ==
=+  l=:(mix (ror 2 a) (ror 13 a) (ror 22 a))              ::  s0
=+  m=:(mix (dis a b) (dis a c) (dis b c))                ::  maj
=+  n=(sum l m)                                           ::  t2
=+  o=:(mix (ror 6 e) (ror 11 e) (ror 25 e))              ::  s1
=+  p=(mix (dis e f) (dis (inv e) g))                     ::  ch
=+  q=:(sum h o p (wac i kbx) (wac i wox))                ::  t1
$(i +(i), a (sum q n), b a, c b, d c, e (sum d q), f e, g f, h g)

