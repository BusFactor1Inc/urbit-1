!:
&.
::
::      Tier 2y, gear assembly
::
++  from
  |=  [a=0* b=0*]
  |%
  ?:  =(a b)
    ~
  [i=a t=..$(a +(a))]
::
++  gear
  |*  [a=nap]
  |=  b=*
  |%
  ?~  b
    ~
  [i=(a -.b) t=..$(b +.b)]
::
++  lone
  |*  a=*
  |%
  [i=a t=~]
::
++  meld
  |*  a=((list %{((gear))}))
  ?~  a
    none
  ?~  t.a
    i.a
  (~(weld go i.a) $(a t.a))
::
++  none
  |%(~) 
::
++  spin
  |*  a=*(list)
  |%
  ?~  a
    ~
  [i=i.a t=..$(a t.a)]
::
++  tulp
  |*  a=*(list)
  ?~  a
    ~|(%tulp !!)
  ?~  t.a
    i.a
  [i.a $(a t.a)]
::
++  zipp
  |*  a=*(list %{nap})
  |=  b=*
  =<  funk
  &.
  +-  funk
    ?~  a
      ~
    [i=(i.a -.b) t=funk(a t.a, b +.b)]
  --
::
++  rack
  |*  a=*(list %{((gear))})
  |%
  =+  ^=  b  =<  $  &.  +-  $  ?~(a ~ [i=(i.a) t=$(a t.a)])  --
  =-  ?.(f.c ~ [i=g.c t=..$(a h.c)])
  ^=  c
  &.  
  +-  f  ?~(b & ?~(i.b | f(b t.b)))
  +-  g  ?~(b !!  ?~(i.b !! ?~(t.b i.i.b [i.i.b g(b t.b)])))
  +-  h  ?~(b ~ ?~(i.b !! [t.i.b h(b t.b)]))
  --
::
::      Tier 2z, gear engine: go
::
++  go
  &=  a=((gear))
  ::
  +-  bark  (drip ~)
  +-  bask  (drop ~)
  +-  beak
    |=  b=@
    ^-  a
    ?:  =(0 b)
      ~
    =+  c=(a)
    ?~  c
      ~|(%slag !!) 
    [i=i.c t=..$(a t.c, b (dec b))]
  ::
  +-  drip
    |=  b=^-(|-(=+(b=(a) ?~(b ~ [i=i.b t=$(a t.b)]))) ~)
    ^-  b
    =+  c=(a)
    ?~  c
      b
    [i=i.c t=$(a t.c)]
  ::
  +-  drop
    |=  b=^-(|-(=+(b=(a) ?~(b ~ [i=i.b t=$(a t.b)]))) ~)
    ^-  b
    =+  c=(a)
    ?~  c
      b
    $(b [i=i.c t=b], a t.c)
  ::
  +-  drum
    |=  b=@
    =+  c=a
    |%  ^-  (a)
    =+  d=(a)
    ?~  d
      ?:  =(0 b)
        ~
      $(a c, b (dec b))
    [i=i.d t=..$(a t.d)]
  ::
  +-  lent
    =+  b=0*
    |-  ^-  0*
    =+  c=(a)
    ?~  c
      b
    $(b +(b), a t.c)
  ::
  +-  mall
    |*  b=bit
    |-  ^-  ?
    =+  c=(a)
    ?~(c & &((b i.c) $(a t.c)))
  ::
  +-  many
    |*  b=bit
    |-  ^-  ?
    =+  c=(a)
    ?~(c | |((b i.c) $(a t.c)))
  ::
  +-  mill
    |*  b=nap
    |%
    =+  c=(a)
    ?~  c
      ~
    [i=(b i.c) t=..$(a t.c)]
  ::
  +-  roll
    |*  b=|=([p=* q=*] q)
    ^-  q.b
    =+  c=(a)
    ?~  c
      q.b
    $(q.b (b i.c q.b), a t.c)
  ::
  +-  reel
    |*  b=|=([p=* q=*] q)
    ^-  q.b
    =+  c=(a)
    ?~  c
      q.b
    (b i.c $(a t.c))
  ::
  +-  snag
    |=  b=@
    =+  c=(a)
    ^-  i.-.c
    ?~  c
      ~|(%snag !!) 
    ?:(=(0 b) i.c $(a t.c, b (dec b)))
  ::
  +-  slag
    |=  b=@
    ^-  a
    ?:  =(0 b)
      a
    =+  c=(a)
    ?~  c
      ~|(%slag !!) 
    $(a t.c, b (dec b))
  ::
  +-  tone
    =+  b=a
    |%
    =+  c=(a)
    ?~  c
      $(a b)
    [i=i.c t=..$(a t.c)]
  ::
  +-  weld
    |*  b=((gear))
    |%  
    =+  c=(a)
    ?~  c
      (b)
    [i=i.c t=..$(a t.c)]
  --
--
